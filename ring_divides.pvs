%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brasil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brasil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brasil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brasil
%%----------------------------------------------------------------------------
%%
%%	Formalization of properties involving the relation "divides"
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

ring_divides [T: TYPE+, +,*:[T,T->T],zero,one:T ]: THEORY

  BEGIN

   ASSUMING IMPORTING ring_with_one_def[T,+,*,zero,one]
    
	fullset_is_ring_with_one: ASSUMPTION ring_with_one?(fullset[T])

   ENDASSUMING
 
  IMPORTING ring_divides_def[T,+,*,zero],
  	        ring_with_id_one_generator[T,+,*,zero,one],
	          ring_unit_def[T,+,*,zero,one],
	          integral_domain_def[T,+,*,zero],
	          ring_nz_closed_aux[T,+,*,zero],
	          integral_domain_with_one_def[T,+,*,zero,one],
	          op_finseq[T,*,one]

   R: VAR ring_with_one
   a, b,u,r,x: VAR T

%%----------------------------------------------------------------------------

  % @QED divides_subset proved by thaynaradelima on Thu, 16 Feb 2023 20:39:31 GMT
  divides_subset: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), a:(remove(zero,R)),b:(R)):
       divides?(R)(a,b) IFF subset?(one_gen(R)(b),one_gen(R)(a))

  % @QED divides_equal proved by galdino on Fri, 17 Feb 2023 13:01:16 GMT
  divides_equal: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), a,b:(remove(zero,R))):
       associates?(R)(a,b) IFF one_gen(R)(b) = one_gen(R)(a)

  % @QED associates_equiv_relation proved by thaynaradelima on Thu, 16 Feb 2023 20:55:05 GMT
  associates_equiv_relation: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R)):
       equivalence?[(remove(zero,R))](associates?(R))

  % @QED unit_divides proved by galdino on Fri, 17 Feb 2023 13:14:48 GMT
  unit_divides: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), u:(remove(zero,R))):
       unit?(R)(u) IFF (FORALL(r:(R)): divides?(R)(u,r))

  % @QED one_gen_unit_R proved by galdino on Fri, 17 Feb 2023 13:21:26 GMT
  one_gen_unit_R: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), u:(remove(zero,R))):
       unit?(R)(u) IFF one_gen(R)(u) = R

  % @QED quot_unit_associates proved by galdino on Fri, 17 Feb 2023 13:24:38 GMT
  quot_unit_associates: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), a,b:(remove(zero,R)), r:(R)):
      (unit?(R)(r) AND a = b*r) IMPLIES associates?(R)(a,b)

  % @QED int_domain_assoc_quot_unit proved by galdino on Fri, 17 Feb 2023 13:30:46 GMT
  int_domain_assoc_quot_unit: LEMMA
   FORALL(R: (integral_domain_w_one?), a,b:(remove(zero,R)), r:(R)):
      (a = b*r AND associates?(R)(a,b)) IMPLIES unit?(R)(r)

  x_divides_x: LEMMA
   FORALL (x:(remove(zero,R))): divides?(R)(x,x)

  int_domain_assoc_unit: LEMMA
    FORALL(R: (integral_domain_w_one?), a,b:(remove(zero,R))):
     associates?(R)(a,b) AND unit?(R)(a) IMPLIES unit?(R)(b)

  div_member_fseq_div_op_finseq: LEMMA
    FORALL (R : (commutative_ring_with_one?), fs : finseq[(R)], c:(remove(zero,R)) ) : 
        FORALL (i: below[length(fs)]) : ( divides?(R)(c,fs(i)) IMPLIES divides?(R)(c,op_fseq(fs)) )
  

  END ring_divides
%%----------------------------------------------------------------------------
