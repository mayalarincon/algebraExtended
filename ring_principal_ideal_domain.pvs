%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brasil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brasil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brasil
%%
%%		André Camapum Carvalho de Freitas
%% 		Universidade Federal de Goiás - Brasil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brasil
%%----------------------------------------------------------------------------
%%
%%	Formalization of properties about principal ideal domains 
%%
%%	Last version: September 10, 2020
%%
%%----------------------------------------------------------------------------
ring_principal_ideal_domain [T:TYPE+, +:[T,T -> T] , *:[T,T -> T], zero:T, one:T]: THEORY

  BEGIN

   ASSUMING IMPORTING ring_with_one_def[T,+,*,zero,one]

          fullset_is_ring_with_one: ASSUMPTION ring_with_one?(fullset[T])

   ENDASSUMING

  IMPORTING ring_with_one_maximal_ideal[T,+,*,zero,one],
  	        ring_principal_ideal_domain_def[T,+,*,zero,one],
	          ring_principal_ideal[T,+,*,zero],
	          ring_with_one_ideal[T,+,*,zero,one],
	          ring_nz_closed_aux[T,+,*,zero],
	          ring_unit[T,+,*,zero,one],
	          %ring_with_id_one_generator [T, +, *, zero,one],
            ring_divides[T,+,*,zero,one],
            ring_prime_element[T,+,*,zero,one] 

  R,M: VAR set[T]
  a,p: VAR T
  i,j: VAR nat
  PID_maximal_prime_ideal: LEMMA
  	FORALL (R:(principal_ideal_domain?)): (M /= singleton(zero)
	       IMPLIES (maximal_ideal?(M,R) IFF prime_ideal?(M,R)))

  el_max_iff_one_gen_maximal: LEMMA
    FORALL(R:(principal_ideal_domain?), c:(R)):
      (LET S = {X:set[T] | (EXISTS (a:(R)): X = principal_ideal(R)(a)) AND X /= singleton(zero) AND  X /= R} IN
     	 ((member(one_gen(R)(c),S) AND FORALL(X: (S)): subset?(one_gen(R)(c), X) IMPLIES one_gen(R)(c) = X)
     	     IFF (c /= zero AND maximal_ideal?(one_gen(R)(c),R))))

  % @QED PID_prime_el_iff_irreducible proved by thaynaradelima on Thu, 24 Feb 2022 20:08:06 GMT
  PID_prime_el_iff_irreducible: LEMMA
   FORALL(R:(principal_ideal_domain?), p:(R)): R_prime_element?(R)(p) IFF R_irreducible_element?(R)(p)
             
  % @QED nonzero_nonunit_irreducible_divides proved by thaynaradelima on Thu, 24 Feb 2022 21:20:00 GMT
  nonzero_nonunit_irreducible_divides: LEMMA
    FORALL (R:principal_ideal_domain, a: (R)): (a/= zero AND NOT unit?(R)(a))
      IMPLIES (EXISTS (c:(R)): (R_irreducible_element?(R)(c) AND divides?(R)(c,a)))

 non_fact_el_set(R: principal_ideal_domain): set[T] = 
 {x: T | member(x,R) AND x/= zero AND NOT unit?(R)(x) AND 
 NOT EXISTS(fsI:(fsIr?(R))): x =  op_fseq(fsI)}

 % @QED empty_non_fact_el_set_aux1 proved by thaynaradelima on Mon, 28 Feb 2022 22:45:04 GMT
 empty_non_fact_el_set_aux1: LEMMA
  FORALL (R:principal_ideal_domain, a: (non_fact_el_set(R))):
    EXISTS (x:(non_fact_el_set(R))): strict_subset?(one_gen(R)(a),one_gen(R)(x))

phi(n:nat, R:principal_ideal_domain, a:(non_fact_el_set(R))): RECURSIVE  (non_fact_el_set(R))    = 
  IF n = 0 then a
    ELSE  choose ({x : (non_fact_el_set(R)) | strict_subset?(one_gen(R)(phi(n-1, R, a)),one_gen(R)(x))})
  ENDIF
  MEASURE n


% @QED empty_non_fact_el_set_aux2 proved by thaynaradelima on Thu, 03 Mar 2022 20:10:08 GMT
empty_non_fact_el_set_aux2: LEMMA
  FORALL (R:principal_ideal_domain, a: (non_fact_el_set(R))):
   EXISTS (phi:[nat -> (non_fact_el_set(R))]): FORALL (i: nat): 
    strict_subset?(one_gen(R)(phi(i)),one_gen(R)(phi(i+1)))

% @QED empty_non_fact_el_set_aux3 proved by thaynaradelima on Thu, 03 Mar 2022 20:27:56 GMT
empty_non_fact_el_set_aux3: LEMMA
  FORALL (R:principal_ideal_domain, a: (non_fact_el_set(R))):
   EXISTS (phi:[nat -> (non_fact_el_set(R))]): FORALL (i: nat, j: nat | i < j): 
    strict_subset?(one_gen(R)(phi(i)),one_gen(R)(phi(j)))    
   

 % @QED empty_non_fact_el_set proved by thaynaradelima on Fri, 04 Mar 2022 13:24:41 GMT
 empty_non_fact_el_set: LEMMA
    FORALL (R:principal_ideal_domain): empty?(non_fact_el_set(R))
    
 % @QED PID_factorization_existence proved by thaynaradelima on Fri, 25 Feb 2022 20:11:37 GMT
 PID_factorization_existence : LEMMA
    FORALL (R:principal_ideal_domain):
     (FORALL(a: (R)): a /= zero AND NOT unit?(R)(a) IMPLIES
			EXISTS(fsI:(fsIr?(R))): a =  op_fseq(fsI))           
  

 % @QED PID_factorization_uniqueness proved by galdino on Fri, 18 Mar 2022 19:55:36 GMT
 PID_factorization_uniqueness: LEMMA
   FORALL (R:principal_ideal_domain):
    FORALL(a: (R), fsIp, fsIq:fsIr(R)): a /= zero AND NOT unit?(R)(a) AND
       a = op_fseq(fsIp) AND a = op_fseq(fsIq) IMPLIES 
           length(fsIp) = length(fsIq) AND
 	   EXISTS(phi:[below[length(fsIp)]->below[length(fsIp)]]):
	      (bijective?(phi)) AND
 	      FORALL(i:below[length(fsIp)]): associates?(R)(fsIq(i),fsIp(phi(i)))  



 END ring_principal_ideal_domain

 
